plugins {
    alias(libs.plugins.androidApplication)
}

//获取key文件
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    namespace 'com.gg.busStation'
    compileSdk 34

    defaultConfig {
        applicationId "com.gg.busStation"
        minSdk 28
        targetSdk 34
        versionCode Integer.parseInt(new Date().format('yyyyMMdd'))
        versionName "1.0.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    splits {
        abi {
            def isReleaseBuild = false

            gradle.startParameter.taskNames.find {
                // Enable split for release builds in different build flavors
                // (assemblePaidRelease, assembleFreeRelease, etc.).
                if (it ==~ /:app:assemble.*Release/) {
                    isReleaseBuild = true
                    return true // break
                }

                return false // continue
            }

            enable isReleaseBuild
            reset()
            include 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
            universalApk true
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            def appName = "HKBusNow"
            def versionCode = variant.versionCode
            def buildType = variant.buildType.name
            def arch = output.getFilter(com.android.build.OutputFile.ABI)
            if (arch == null) {
                arch = "universal"
            }
            outputFileName = "${appName}_${versionCode}_${buildType}_${arch}.apk"
        }
    }

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']

//            v1SigningEnabled true
            v2SigningEnabled true
            enableV3Signing true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            signingConfig signingConfigs.config
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }
}

dependencies {
    // Baidumap: https://lbsyun.baidu.com
//    implementation libs.baidumapsdk.base
    implementation libs.baidumapsdk.util
    implementation libs.baidumapsdk.location
    //Gson: https://github.com/google/gson
    implementation libs.gson
    //OKHttp: https://github.com/square/okhttp
    implementation libs.okhttp

    implementation libs.appcompat
    // Material design: https://material.io
    implementation libs.material
    implementation libs.navigation.fragment
    implementation libs.navigation.ui
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}